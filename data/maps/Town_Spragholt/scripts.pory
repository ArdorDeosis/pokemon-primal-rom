// VAR_GAME_INTRO_STAGE
const TRIED_TO_LEAVE_TOWN = 1
const TALKED_TO_SHAMAN = 2
const CHOSE_POKEMON = 3
const GOT_SENT_TO_BRAKSTRAND = 4
const GOT_SENT_TO_COLLECT_APRICORN = 5
const HANDED_OVER_APRICORN = 6
const PICKED_UP_POKEBALL = 7
const DELIVERED_POKEBALL = 8

const NORTH = 1
const SOUTH = 2
const WEST = 3
const EAST = 4

const JUMPED_FIRE = FLAG_TEMP_1
const HIDE_SHAMAN = FLAG_TEMP_2
const HIDE_FATHER = FLAG_TEMP_3

const EXITING_TILE = VAR_TEMP_1
const LAST_TILE_NEXT_TO_FIRE = VAR_TEMP_2

mapscripts Town_Spragholt_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Town_Spragholt_SetupMap
}

script Town_Spragholt_SetupMap {
    setflag(HIDE_SHAMAN)
    setflag(HIDE_FATHER)
}


// === FIRE MAN ===

script Town_Spragholt_EventScript_FireMan {
    lock
    faceplayer
    if(flag(JUMPED_FIRE)){
        msgbox(format("BY THE GREAT PILOWSWINE! You jumped the fire!\pSo...\pFAST or CRISPY butt?"))
        multichoice(22, 8, MULTI_FAST_OR_CRISPY, TRUE)
        msgbox(format("Thought as much..."))
        clearflag(JUMPED_FIRE)
    }
    else {
        msgbox(format("Cool cave dwellers can make fire. But REALLY cool cave dwellers...\pthey dare to jump over it!\pYou'll need a FAST butt, or you get a CRISPY butt.\pDare you?"))
    }
    release
    end
}

script Town_Spragholt_Trigger_AwayFromFire {
    setvar(LAST_TILE_NEXT_TO_FIRE, 0)
} 

script Town_Spragholt_Trigger_StandAtFireNorth {
    if(var(LAST_TILE_NEXT_TO_FIRE) == SOUTH) {
        setflag(JUMPED_FIRE)
    }
    setvar(LAST_TILE_NEXT_TO_FIRE, NORTH)
}

script Town_Spragholt_Trigger_StandAtFireEast {
    if(var(LAST_TILE_NEXT_TO_FIRE) == WEST) {
        setflag(JUMPED_FIRE)
    }
    setvar(LAST_TILE_NEXT_TO_FIRE, EAST)
}

script Town_Spragholt_Trigger_StandAtFireSouth {
    if(var(LAST_TILE_NEXT_TO_FIRE) == NORTH) {
        setflag(JUMPED_FIRE)
    }
    setvar(LAST_TILE_NEXT_TO_FIRE, SOUTH)
}

script Town_Spragholt_Trigger_StandAtFireWest {
    if(var(LAST_TILE_NEXT_TO_FIRE) == EAST) {
        setflag(JUMPED_FIRE)
    }
    setvar(LAST_TILE_NEXT_TO_FIRE, WEST)
}

// === GAME INTRO ===

script Town_Spragholt_Trigger_TryToLeave_North {
    setvar(EXITING_TILE, NORTH)
    if(var(VAR_GAME_INTRO_STAGE) <= CHOSE_POKEMON) {
        call(TryToLeaveWithoutPokemon)
    }
    elif (var(VAR_GAME_INTRO_STAGE) >= DELIVERED_POKEBALL && !flag(FLAG_SYS_POKEDEX_GET)) {
        call(TryToLeaveWithoutPokedex)
    }
}

script Town_Spragholt_Trigger_TryToLeave_South {
    setvar(EXITING_TILE, SOUTH)
    if(var(VAR_GAME_INTRO_STAGE) <= CHOSE_POKEMON) {
        call(TryToLeaveWithoutPokemon)
    }
    elif (var(VAR_GAME_INTRO_STAGE) >= DELIVERED_POKEBALL && !flag(FLAG_SYS_POKEDEX_GET)) {
        call(TryToLeaveWithoutPokedex)
    }
}

script(local) TryToLeaveWithoutPokemon {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark))
    waitmovement
    msgbox(format("Wait!"), MSGBOX_NPC)
    clearflag(HIDE_SHAMAN)
    addobject(LOCALID_SPRAGHOLT_TOWN_SHAMAN)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_right))
    switch (var(EXITING_TILE)) {
        case NORTH:
            applymovement(LOCALID_SPRAGHOLT_TOWN_SHAMAN, moves(walk_slow_up * 7, walk_slow_left * 6))
        case SOUTH:
            applymovement(LOCALID_SPRAGHOLT_TOWN_SHAMAN, moves(walk_slow_up * 6, walk_slow_left * 6))
    }
    waitmovement
    msgbox(format(
        "Where do you think you're waddling off to?\pYou try sneakin' off like that again, and I swear I'll tie a rope 'round your ankle.\p"
        "Get in here and act like you've got a brain cell or two.\pYou've got business to finish."
    ))

    
    switch (var(EXITING_TILE)) {
        case NORTH:
            applymovement(LOCALID_SPRAGHOLT_TOWN_SHAMAN, moves(
                walk_slow_right * 6,
                walk_slow_down * 8,
                walk_slow_left * 3,
                walk_slow_up,
                set_invisible
            ))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(
                walk_slow_right,
                walk_slow_right,
                delay_16 * 2, walk_right * 2,
                delay_16, walk_right,
                delay_8, walk_right, delay_8,
                delay_16, walk_right, face_down,
                delay_16 * 2, walk_down * 2,
                face_right, delay_16, face_left,
                walk_down * 3,
                delay_16 * 3, walk_down * 2,
                delay_16, walk_down,
                face_right,
                delay_16 * 2, walk_left * 2,
                delay_16, walk_left,
                walk_up
            ))
        case SOUTH:
            applymovement(LOCALID_SPRAGHOLT_TOWN_SHAMAN, moves(
                walk_slow_right * 6,
                walk_slow_down * 7,
                walk_slow_left * 3,
                walk_slow_up,
                set_invisible
            ))
            applymovement(OBJ_EVENT_ID_PLAYER, moves(
                walk_slow_right,
                walk_slow_right,
                delay_16 * 2, walk_right * 2,
                delay_16, walk_right,
                delay_8, walk_right, delay_8,
                delay_16, walk_right, face_down,
                delay_16 * 2, walk_down * 2,
                face_right, delay_16, face_left,
                walk_down * 3,
                delay_16 * 2, walk_down,
                delay_16, walk_down,
                face_right,
                delay_16 * 2, walk_left * 2,
                delay_16, walk_left,
                walk_up
            ))
    }
    waitmovement
    setvar(VAR_GAME_INTRO_STAGE, 1)
    warp(MAP_SPRAGHOLT_SHAMAN_CAVE, 0)
    releaseall
    end
}


script(local) TryToLeaveWithoutPokedex {
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark))
    waitmovement
    clearflag(HIDE_FATHER)
    addobject(ID_Father_Spragholt)
    if (var(EXITING_TILE) == NORTH) {
        // setobjectxyperm(ID_Father_Spragholt, 8, 6) // apparently does not work...
        applymovement(ID_Father_Spragholt, moves(walk_faster_up))
    }
    msgbox(format("{PLAYER}!"), MSGBOX_NPC)
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_right))
    applymovement(ID_Father_Spragholt, moves(walk_fast_left * 7))
    waitmovement

    call(Spragholt_Pokedex_Speech_Part1)

    applymovement(ID_Father_Spragholt, moves(
        lock_facing_direction,
        walk_right,
        unlock_facing_direction
    ))
    waitmovement
    
    call(Spragholt_Pokedex_Speech_Part2)
    
    applymovement(ID_Father_Spragholt, moves(
        walk_right * 5,
        walk_down * 2,
        walk_right * 2
    ))
    waitmovement
    releaseall
}