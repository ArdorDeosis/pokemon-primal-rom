const HIDE_FOLLOWING_POKEMON = FLAG_TEMP_1
const HIDE_TURTWIG = FLAG_TEMP_2
const HIDE_STARTER2 = FLAG_TEMP_3
const HIDE_STARTER3 = FLAG_TEMP_4

const X_COORD = VAR_TEMP_1
const Y_COORD = VAR_TEMP_2

// VAR_GAME_INTRO_STAGE
const TRIED_TO_LEAVE_TOWN = 1
const TALKED_TO_SHAMAN = 2
const CHOSE_POKEMON = 3
const RECEIVED_POKEMON = 4
const GOT_SENT_TO_BRAKSTRAND = 5
const GOT_SENT_TO_COLLECT_APRICORN = 6
const HANDED_OVER_APRICORN = 7
const PICKED_UP_POKEBALL = 8
const DELIVERED_POKEBALL = 9


mapscripts Spragholt_ShamanCave_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Spragholt_ShamanCave_SetupCave
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_GAME_INTRO_STAGE, TRIED_TO_LEAVE_TOWN: GettingDraggedIn
	]
}

script(local) Spragholt_ShamanCave_SetupCave {
    // move shaman to entrance when being dragged in
    if(var(VAR_GAME_INTRO_STAGE) == TRIED_TO_LEAVE_TOWN) {
        setobjectxyperm(LOCALID_SHAMAN, 16, 16)
        setobjectmovementtype(LOCALID_SHAMAN, MOVEMENT_TYPE_FACE_UP)
    }
    // hide what needs to be hidden
    setflag(HIDE_FOLLOWING_POKEMON)
}


script Spragholt_ShamanCave_Shaman {
    lockall
    faceplayer
    if (var(VAR_GAME_INTRO_STAGE) < RECEIVED_POKEMON) {
        msgbox(format(
            "Still here?\pWhat, need me to hold your hand?\nThey're behind me!\p"
            "Big ones, small ones... all with teeth and attitude.\p"
            "Pick one, before they start pickin' each other."
        ), MSGBOX_NPC)
        waitmessage
    } 
    elif (var(VAR_GAME_INTRO_STAGE) >= RECEIVED_POKEMON && var(VAR_GAME_INTRO_STAGE) < GOT_SENT_TO_COLLECT_APRICORN) {
        msgbox(format(
            "Do you have my Poké Ball yet? No? "
            "Then what are you flappin' your gums here for? Go on!"
        ), MSGBOX_NPC)
        waitmessage
    }
    elif (var(VAR_GAME_INTRO_STAGE) >= GOT_SENT_TO_COLLECT_APRICORN && var(VAR_GAME_INTRO_STAGE) < DELIVERED_POKEBALL) {
        msgbox(format(
            "You look like a bag of chewed roots. "
            "Sit still for a second."
        ))
        waitmessage
        // TODO: heal script
        if(checkitem(ITEM_ISOLDES_POKEBALL)){
            msgbox(format(
                "Ahh, there it is. My Poké Ball, finally. Off you go now.\p"
                "Maybe go see your father before you vanish into the grass."
            ))
            waitmessage
            removeitem(ITEM_ISOLDES_POKEBALL)
            setvar(VAR_GAME_INTRO_STAGE, DELIVERED_POKEBALL)
        } else {
            msgbox(format("Still no Poké Ball? Then what are you even doing here? Shoo!"))
            waitmessage
        }
    }
    releaseall
}

script Spragholt_ShamanCave_StarterTurtwig {
    lockall
    showmonpic(SPECIES_TURTWIG, 10, 3)
    playmoncry(SPECIES_TURTWIG, CRY_MODE_NORMAL)
    msgbox(format("It tries to bite you, when you approach.\pTake Turtwig?"), MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        setfollowernpc(ID_CAVE_STARTER_TURTWIG, FNPC_NONE)
        setvar(VAR_GAME_INTRO_STAGE, CHOSE_POKEMON)
    }
    hidemonpic
    
    releaseall
    end
}

script(local) ChosePokemonSpeech {
    msgbox(format(
        "Your father's not thrilled. Didn't say much, but he didn't need to.\p"
        "He's not angry at you. Not really.\pJust doesn't know what to do with all that worry.\p"
        "Don't hold it against him.\pWatching your child walk toward danger... no easy thing.\p"
        "But here you are anyway. Stubborn as dried clay.\p"
        "Behind me are three Pokémon. They'll test you, trip you, maybe bite you.\p"
        "Pick one.\pAnd if you drop it on your foot, don't come limping back to me.\p"
        "Go on, before I change my mind and make you take the loud one."
    ))
    setvar(VAR_GAME_INTRO_STAGE, TALKED_TO_SHAMAN)
    waitmessage
}

script Spragholt_ShamanCave_Trigger_LowerFloorBottleneck {
    if (var(VAR_GAME_INTRO_STAGE) < TALKED_TO_SHAMAN){
        call(GettingCalledOver)
    } elif (var(VAR_GAME_INTRO_STAGE) == TALKED_TO_SHAMAN){
        call(GettingCalledBack)
    }
}

script Spragholt_ShamanCave_Trigger_PokemonCaveEntrance {
    if (var(VAR_GAME_INTRO_STAGE) == CHOSE_POKEMON){
        lockall
        applymovement(LOCALID_SHAMAN, moves(face_right))
        waitmovement
        msgbox(format("So you picked Turtwig, huh?\pWell, someone's gotta love the grumpy ones.\pLet me help you with this..."))
        waitmessage

        applymovement(LOCALID_SHAMAN, moves(walk_slow_up, walk_in_place_slow_right))
        waitmovement
        applymovement(OBJ_EVENT_ID_NPC_FOLLOWER, moves(enter_pokeball))
        waitmovement
        destroyfollowernpc
        applymovement(LOCALID_SHAMAN, moves(walk_slow_down, walk_in_place_slow_right))
        waitmovement
        msgbox(format("There you go."))
        waitmessage
        
        
        givemon(SPECIES_TURTWIG, 5, ITEM_NONE, ITEM_WEAK_POKEBALL)
        if (var(VAR_RESULT) == MON_GIVEN_TO_PARTY) {
            setvar(VAR_GAME_INTRO_STAGE, RECEIVED_POKEMON)
        }
        setflag(FLAG_SYS_POKEMON_GET)
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox("You received Turtwig!")
        waitmessage
        msgbox(format(
            "Poké Balls aren't easy to come by, y'know. I had Korken, the Poké Ball Crafter over in Brakstrand, make me a new one.\p"
            "Go fetch it, would ya?\nAnd don't dawdle - if I fall asleep waitin', I might not wake up!"
        ))
        waitmessage
        setvar(VAR_GAME_INTRO_STAGE, GOT_SENT_TO_BRAKSTRAND)
        applymovement(LOCALID_SHAMAN, moves(walk_slow_down, walk_in_place_slow_left))
        waitmovement
        releaseall
    }
}

script(local) GettingDraggedIn {
    lockall
    applymovement(LOCALID_SHAMAN, moves(
        walk_slow_left,
        walk_slow_up,
        walk_slow_left * 2,
        walk_slow_down
        step_end
    ))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(
        delay_16 * 2,
        walk_left,
        walk_up,
        delay_16, walk_up,
        delay_16 * 2, walk_left * 2,
        walk_in_place_down
    ))
    waitmovement
    msgbox(format("Why do I...\pthe oldest person in town...\plive in the only cave...\pwith FUCKING STAIRS!"))
    waitmessage
    applymovement(LOCALID_SHAMAN, moves(
        walk_slow_down,
        walk_slow_left * 2,
        walk_slow_up,
        walk_slow_left,
        walk_slow_up,
        walk_slow_up,
        walk_slow_left * 3,
        face_down,
        step_end
    ))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(
        face_left,
        delay_16 * 5,
        walk_down * 2,
        walk_in_place_left,
        delay_16 * 2,
        walk_left * 3,
        face_up,
        delay_16 * 2,
        walk_left, walk_up,
        walk_left, walk_up,
        walk_left,
        face_up,
        step_end
    ))
    waitmovement
    call(ChosePokemonSpeech)
    releaseall
}

script(local) GettingCalledOver {
    lockall
    applymovement(LOCALID_SHAMAN, moves(emote_exclamation_mark))
    waitmovement
    msgbox(format(
        "Ah, there you are.\pTook you long enough - my joints are older than this cave, ya know.\p"
        "Come on, move those legs before I die."
    ))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, moves(
        walk_left * 4,
        walk_up * 2,
        walk_left,
        face_up,
        step_end
    ))
    waitmovement
    call(ChosePokemonSpeech)
    releaseall
}

script(local) GettingCalledBack {
    lockall
    msgbox(format(
        "Where do you think you're waddling off to?\pThe Pokémon are right behind me, genius.\p"
        "Go pick one before I assign you the loudest one on purpose."
    ))
    waitmessage
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left, step_end))
    waitmovement
    releaseall
}