const HIDE_POKEBALL_ON_TABLE = FLAG_TEMP_2

// VAR_GAME_INTRO_STAGE
const TRIED_TO_LEAVE_TOWN = 1
const TALKED_TO_SHAMAN = 2
const CHOSE_POKEMON = 3
const GOT_SENT_TO_BRAKSTRAND = 4
const GOT_SENT_TO_COLLECT_APRICORN = 5
const HANDED_OVER_APRICORN = 6
const PICKED_UP_POKEBALL = 7
const DELIVERED_POKEBALL = 8


mapscripts Brakstrand_PokeBallCrafterCave_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SetupCave
}


script(local) SetupCave {
    // move crafter in front of the table
    if (var(VAR_GAME_INTRO_STAGE) <= GOT_SENT_TO_BRAKSTRAND || 
        (var(VAR_GAME_INTRO_STAGE) == GOT_SENT_TO_COLLECT_APRICORN && checkitem(ITEM_APRICORN) == FALSE)) {
        setobjectxyperm(LOCALID_BALL_CRAFTER, 3, 3)
        setobjectmovementtype(LOCALID_BALL_CRAFTER, MOVEMENT_TYPE_FACE_UP)
    }
    // remove pokeball on the table
    if (var(VAR_GAME_INTRO_STAGE) >= PICKED_UP_POKEBALL) {
        setflag(HIDE_POKEBALL_ON_TABLE)
        removeobject(LOCALID_POKEBALL)
    }
}


script Brakstrand_PokeBallCrafterCave_Crafter {
    lockall
    faceplayer
    switch (var(VAR_GAME_INTRO_STAGE)) {
        case GOT_SENT_TO_BRAKSTRAND:
            call(CrafterInteraction_Introduction)
        case GOT_SENT_TO_COLLECT_APRICORN:
            call(CrafterInteraction_CollectApricorn)
        case HANDED_OVER_APRICORN:
            call(CrafterInteraction_TakePokeball)
        default:
            call(CrafterInteraction_Default)

    }
    releaseall
}


script(local) CrafterInteraction_Default {
    msgbox(format("Poké Ball crafting is not implemented yet!"))
    waitmessage
}


script(local) CrafterInteraction_Introduction {
    msgbox(format(
        "Eh? What do you want?\p...\p...\p"
        "Oh! Poké Balls. Yes. Yes! I craft those!\p"
        "From Apricorns, of course. But it takes time and... passion.\p...\p"
        "So you're the one Isolde sent. She said someone would come for her Poké Ball.\p"
        "Lucky for you, I've been fondling that ball all morning. Nearly done!\p"
        "Bring me an Apricorn and it's yours.\p"
        "You'll find Apricorn trees just south of here, lounging near the beach like lazy Slakoth."
    ))
    waitmessage
    setvar(VAR_GAME_INTRO_STAGE, GOT_SENT_TO_COLLECT_APRICORN)
}


script(local) CrafterInteraction_CollectApricorn {
    if(checkitem(ITEM_APRICORN)) {
        call(HandOverApricorn)
    }
    else {
        faceplayer
        msgbox(format(
            "Did you get me an Apricorn?\p"
            "Trees south of here should have some - assuming the wind hasn't talked them into hiding."
        ))
        waitmessage
        setobjectmovementtype(LOCALID_BALL_CRAFTER, MOVEMENT_TYPE_FACE_UP)
    }
}


script(local) CrafterInteraction_TakePokeball {
    faceplayer
    msgbox(format(
        "The Poké Ball on the table is yours.\p"
        "Careful, though - it's freshly hardened. Let it cool a moment before tossing it around."
    ))
    waitmessage
}


script Brakstrand_PokeBallCrafterCave_Pokeball {
    lockall
    if (var(VAR_GAME_INTRO_STAGE) < HANDED_OVER_APRICORN){
        applymovement(LOCALID_BALL_CRAFTER, moves(walk_in_place_fast_left, emote_question_mark))
        waitmovement
        msgbox(format(
            "Eh! Hands off unless you've brought me an Apricorn!\p"
            "...Wait, you have? Well then - hand it over!"
        ))
        waitmessage
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_down))
        waitmovement
        call(HandOverApricorn)
    } else {
        giveitem(ITEM_ISOLDES_POKEBALL)
        removeobject(LOCALID_POKEBALL)
        setflag(HIDE_POKEBALL_ON_TABLE)
        setvar(VAR_GAME_INTRO_STAGE, PICKED_UP_POKEBALL)
    }
    releaseall
}


script(local) HandOverApricorn {
    if(checkitem(ITEM_APRICORN, 2)) {
        msgbox(format(
            "Oho! A fine bunch of Ripe Apricorns. Juicy little things."
            "I'll take one, thank you very much!"
        ))
    } else {
        msgbox(format("Ah. A lone Ripe Apricorn. Juicy little thing."))
    }
    msgbox(format(
        "I can make higher quality Poké Balls from Firm Apricorns - the ones that aren't quite ripe yet.\p"
        "But this one will have to do, I guess."
    ))
    waitmessage
    removeitem(ITEM_APRICORN)
    setvar(VAR_GAME_INTRO_STAGE, HANDED_OVER_APRICORN)
    call(CrafterInteraction_TakePokeball)
    msgbox(format("My regards to Isolde!\p And bring more Apricorns if you want more Poké Balls made."))
    waitmessage
}